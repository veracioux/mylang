%import common.ESCAPED_STRING -> DOUBLE_QUOTED_STRING
%import common.SIGNED_NUMBER
%import common.WS_INLINE -> _WS_INLINE
%import common.WS -> _WS
%import common.NEWLINE -> _NEWLINE
%import common._STRING_ESC_INNER
%import common.SH_COMMENT


// Terminals

SINGLE_QUOTED_STRING : "'" _STRING_ESC_INNER "'"
BOOL: "true" | "false"
NULL: "null"
UNDEFINED: "undefined"
UNQUOTED_STRING: /(?!true\b|false\b|null\b|undefined\b)[a-z_][a-z0-9_]*/i
DOTS: /\.+/
_OPERATOR_SYMBOL: /[+\-\*\/%&|^~!<>?:@\$\\`]/
OPERATOR: _OPERATOR_SYMBOL+
        | ("=" (_OPERATOR_SYMBOL | "=")+)
        | ((_OPERATOR_SYMBOL | "=")+ "=")


// Basic literals

_signed_number.2: SIGNED_NUMBER // TODO REMOVE
?string: DOUBLE_QUOTED_STRING | SINGLE_QUOTED_STRING | UNQUOTED_STRING
?primitive: BOOL | NULL | UNDEFINED | SIGNED_NUMBER


// Args

_ARG_SEPARATION_EXPLICIT: _WS_INLINE* "," _WS*
_ARG_SEPARATION: _WS_INLINE+ | _ARG_SEPARATION_EXPLICIT

_arg_and_comma: expression _ARG_SEPARATION_EXPLICIT
_positional_args: (expression (_ARG_SEPARATION expression)+ | _arg_and_comma)
_keyed_args: _WS_INLINE* assignment (_ARG_SEPARATION assignment)* [_ARG_SEPARATION]

_args: _positional_args
     | _keyed_args
     | ((_positional_args | expression) _ARG_SEPARATION _keyed_args)

args.1: _args


// List of statements
_statement: expression | args
_statement_separation: (_WS_INLINE* (";" | _NEWLINE) _WS*)+
// A single expression or multiple statements
statement_list: (_statement? _statement_separation)+ _statement?
_wrapped_statement_list: "(" _WS* statement_list _WS* ")"
execution_block_single_statement: (_positional_args _keyed_args?)
                                | expression
execution_block: "{" _WS* (statement_list | execution_block_single_statement) _WS* "}"


// Data structures

dict.2: "{" _WS* [_keyed_args] _WS_INLINE* "}"
array.1: "(" _WS* [_positional_args] ")"
wrapped_args: "(" _args ")"


// Operations and assignment

// TODO: Assignment could perhaps be a special case of binary operation
assignment: expression _WS_INLINE* "=" _WS_INLINE* expression
prefix_operation: OPERATOR (expr_4 | expr_5)
postfix_operation: (expr_3 | expr_4 | expr_5) OPERATOR
binary_operation: (expression OPERATOR expression)
                | (expression _WS+ OPERATOR _WS+ expression)


// Expression - any expression that can be evaluated to produce a value

// N indicates "tightness" of binding; higher N binds tighter
// This is used to control precedence where Lark's priorities are insufficient.
?expr_1: binary_operation
?expr_2: postfix_operation
?expr_3: prefix_operation
?expr_4: path | dots
?expr_5: ("(" expression ")")
       | string
       | primitive
       | dict
       | array
       | wrapped_args
       | execution_block
       | _wrapped_statement_list

?expression: expr_5
           | expr_4
           | expr_3
           | expr_2
           | expr_1

// TODO: Disallow unparenthesized floats
dots: DOTS
_dot.2: "."
path: (dots? expr_5 ((_dot | dots) (expr_5 | expr_3))+ dots*)
    | (dots expr_5)
    | (expr_5 dots)

module: _WS* (statement_list? | args | expression) _WS*

%ignore SH_COMMENT
